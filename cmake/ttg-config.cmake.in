# - CMAKE Config file for the TTG package

# Set package version
set(TTG_VERSION "@TTG_VERSION@")
set(TTG_EXT_VERSION "@TTG_EXT_VERSION@")

set(PaRSEC_CONFIG "@PaRSEC_CONFIG@")
set(MADNESS_CONFIG "@MADNESS_CONFIG@")
set(Boost_CONFIG "@Boost_CONFIG@")
set(BTAS_CONFIG "@BTAS_CONFIG@")

@PACKAGE_INIT@

if (NOT TARGET MADworld AND MADNESS_CONFIG)
  get_filename_component(MADNESS_CONFIG_DIR "${MADNESS_CONFIG}" DIRECTORY)
  find_package(MADNESS 0.10.1 CONFIG QUIET COMPONENTS world PATHS "${MADNESS_CONFIG_DIR}" NO_DEFAULT_PATH)
endif()

if (NOT TARGET PaRSEC::parsec AND PaRSEC_CONFIG)
  get_filename_component(PaRSEC_CONFIG_DIR "${PaRSEC_CONFIG}" DIRECTORY)
  find_package(PaRSEC CONFIG QUIET COMPONENTS parsec PATHS "${PaRSEC_CONFIG_DIR}" NO_DEFAULT_PATH)
endif()

if (NOT TARGET BTAS::BTAS AND BTAS_CONFIG)
  get_filename_component(BTAS_CONFIG_DIR "${BTAS_CONFIG}" DIRECTORY)
  find_package(BTAS 1.0.0 QUIET CONFIG PATHS "${BTAS_CONFIG_DIR}" NO_DEFAULT_PATH)
endif()

if (NOT TARGET Boost::boost AND Boost_CONFIG)
  get_filename_component(Boost_CONFIG_DIR "${Boost_CONFIG}" DIRECTORY)
  find_package(Boost ${TTG_TRACKED_BOOST_VERSION} CONFIG QUIET OPTIONAL_COMPONENTS serialization PATHS "${Boost_CONFIG_DIR}" NO_DEFAULT_PATH)
endif()

# Include library IMPORT targets
if(NOT TARGET ttg)
  include("${CMAKE_CURRENT_LIST_DIR}/ttg-targets.cmake")
  if(NOT TARGET ttg)
    message(FATAL_ERROR "expected ttg among imported TTG targets")
  endif()
endif()

set(TTG_FOUND TRUE)
